\chapter{The Full AST EBNF}
\label{app:astebnffull}

\footnotesize
\begin{verbatim}
                   <Program> ::= *<Process> *<GlobalVariableDeclaration>
 <GlobalVariableDeclaration> ::= <Name> <InitialExpression>
                   <Process> ::= <Name> *<Block> 
                                 *<ProcessVariableDeclaration>
                                 <EntryBlock>
                      <Name> ::= string
<ProcessVariableDeclaration> ::= <Name> <InitialExpression>
         <InitialExpression> ::= <Expression>
                     <Block> ::= <Name> <Statements>
                <EntryBlock> ::= <Block>
                
                <Statements> ::= *<Statement> |
                                 <Statements> <UnconditionalGotoStatement>
                 <Statement> ::= <LocalVariableDeclaration> |
                                 <ReadStatement> |
                                 <GlobalVariableReadStatement> |
                                 <ReceiveStatement>  |
                                 <WriteStatement> | <SendStatement> |
                                 <GlobalVariableWriteStatement> |
                                 <ConditionalGotoStatement>
             <ReadStatement> ::= <LocalVariableExpression>
                                 <ProcessVariableExpression>
            <WriteStatement> ::= <ProcessVariableExpression> <Expression>
             <SendStatement> ::= <Expression> <ProcessIDString>
          <ReceiveStatement> ::= <LocalVariableExpression>
  <LocalVariableDeclaration> ::= <Name>
  <ConditionalGotoStatement> ::= <Block> <Expression>
<UnconditionalGotoStatement> ::= <Block>
           <ProcessIDString> ::= string
           
                <Expression> ::= <BinaryOperatorExpression> |
                                 <LocalVariableExpression> |
                                 <GlobalVariableExpression> |
                                 <ProcessVariableExpression> |
                                 <UnknownExpression> |
                                 <IntegerConstantExpression> |
                                 <StringExpression> |
                                 <Undefined>
   <LocalVariableExpression> ::= <LocalVariableDeclString>
 <ProcessVariableExpression> ::= <ProcessVariableDeclString>
  <BinaryOperatorExpression> ::= <Expression> <Operator> <Expression>
 <IntegerConstantExpression> ::= integer
         <UnknownExpression> ::= string
                 <Undefined> ::= ""
                  <Operator> ::= + | - | * | /
   <LocalVariableDeclString> ::= string
 <ProcessVariableDeclString> ::= string
\end{verbatim}
\normalsize
