uri "http://hib.no/SADLTest/cpn" alias CPN version "1". 

PetriNet is a class described by page with values of type Page.
HighLevelPetriNet is a type of PetriNet.
ColoredPetriNet is a type of HighLevelPetriNet.

Page is a class 
	described by nodes with values of type Node,
	described by arc with values of type Arc.

Node is a class described by name with a single value of type string.

Place is a type of Node,
	described by initialMarking with a single value of type string,
	described by colorType with a single value of type string.

Socket is a type of Place 
	described by port with a single value of type Port,
	described by direction with a single value of type SocketDirection.
	SocketDirection is a class, must be one of {In, Out, InOut}.
Port is a type of Place described by socket with values of type Socket. 

Transition is a type of Node,
	described by guard with a single value of type string,
	described by timing with a single value of type int,
	described by codeSegment with a single value of type string.

SubstiutionTransition is a type of Transition
	described by page with a single value of type Page.

Arc is a class,
	described by inscription with a single value of type string, 
	described by place with a single value of type Place,
	described by transition with a single value of type Transition,
	described by source with a single value of type Node,
	described by dest with a single value of type Node,
	described by direction with a single value of type ArcDirection.
//direction of Arc must be one of {PlaceToTrans, TranstoPlace, Both}.
ArcDirection is a class, must be one of {PlaceToTrans, TransToPlace, Both}.
Rule ArcRule1
	given x is an Arc
	if direction of x is PlaceToTrans
	then source of x is place of x, dest of x is transition of x.
Rule ArcRule2
	given x is an Arc
	if direction of x is TransToPlace
	then source of x is transition of x, dest of x is place of x.
Rule ArcRule3
	given x is an Arc
	if direction of x is Both
	then source of x is not known, dest of x is not known.
Rule ArcRule4
	given x is an Arc
	if source of x is a Place
	and dest of x is a Transition
	then direction of x is PlaceToTrans.
Rule ArcRule5
	given x is an Arc
	if source of x is a Transition
	and dest of x is a Place
	then direction of x is TransToPlace.

Arc2 is a class,
	described by inscription with a single value of type string, 
	described by source with a single value of type Node,
	described by dest with a single value of type Node,
	described by twoway with a single value of type boolean.
/*Rule ArcPlaceToTrans
	given x is an Arc2
	if source of x is a Place
	then dest of x is a Transition.
Rule ArcTransToPlace
	given x is an Arc2
	if source of x is a Transition
	then dest of x is a Place.*/
	